' Gambas class file

Public Enum client, type
Public Bsel As Boolean = False
Public Bval As String = ""

Private son As Integer = Start.Son
Private Dbl As Boolean = False
Private Col As Collection
Private trb As TriGen
Private $lpx As LPrix
Private $resart As Result
Private $ttc As Float
Private $choix As Integer

Public Sub _new(choix As Integer, Optional cat As String, article As String)

  Dim ch As Chainage
  
  Me.center
  ch = New Chainage([Tart, Tcoeff, Tpvht, Tpvttc, remise, Button6])
  TypeC.MaxLenght = 2
  Tart.MaxLenght = 15
  Tart.recherche = True
  Tart.champs("Fiches_Art", "art_code", "art_design")
  Tart.lke = TextReg.commence_par
  Tart.conteneur = Me
  Tart.RechX = Coltype.X
  Tart.RechY = Button4.Y
  Tprht.numerique = True
  tcoeff.numerique = True
  tcoeff.reg = "-?[0-9]*[,.]?[0-9]{0,4}"
  Tpvht.numerique = True
  Tpvttc.numerique = True
  pvpublic.numerique = True
  remise.numerique = True
  remise.reg = "-?[0-9]*[,.]?[0-9]{0,2}"
  Lstart.Tooltip = "Appuyez sur Supr pour supprimer cette remise"
  
  $choix = choix
  If $choix = client Then
    Me.Text = "Tarif par client"
    TextLabel29.Text = "Client"
    TypeC.MaxLenght = 15
  Endif
  Typec.SetFocus
  If cat Then
    TypeC.Text = cat
    TypeC_man()
    Tart.SetFocus
  Endif
  If article Then
    Tart.Text = article
    Art_Man
  Endif
  
End

'******************************************* Gestion des clients tarifs*************************************************
'**********************************************************
'*      Le bouton sert a rechercher les types clients     *
'**********************************************************
Public Sub Button4_click()

  Dim Rtypec As Result
  Dim trc As TriCpt
  
  If $choix = client Then
    trc = New TriCpt(TriCpt.Client, TriCpt.Sans_Solde, Me)
    trc.ShowModal
    If Bsel Then
      TypeC.Text = Bval
      TypeC_man()
      Tart.SetFocus
    Endif
  Else
    Coltype.Raise
    If Coltype.Visible = True Then
      Coltype.Clear
      Coltype.Visible = False
    Else
      Coltype.visible = True
      Coltype.Columns.count = 2
      Coltype.Columns[0].Width = 65
      Coltype.Columns[1].Width = 80
      Coltype.Columns[0].Text = "code"
      Coltype.Columns[1].Text = "Intitulé"
      Rtypec = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabTypec") & "")
      If Rtypec.Available Then
        Repeat
          Coltype.Add(Rtypec!code, Rtypec!code)
          Coltype.Item[0] = Rtypec!code
          Coltype.Item[1] = Rtypec!libelle
        Until Rtypec.MoveNext()
        Coltype.MoveFirst
        Coltype.SetFocus
        Coltype.Item.Selected = True
      Endif
    Endif
  Endif

End

'******************************************
'*      Selection du  type client         *
'******************************************

Public Sub Typec_LostFocus()

  If Typec.Text Then TypeC_man()

End

Public Sub Typec_KeyPress()

  If Key.Code = Key.Enter Or Key.Code = Key.Return Or Key.Code = Key.Tab Then
    Stop Event
    Tart.SetFocus
  Endif
  
End

Public Sub Coltype_KeyPress()

  If Key.code = Key.Return Or Key.code = Key.Enter Then
    TypeC_man()
    Tart.SetFocus
  Endif
  If Key.Code = Key.Esc Then Coltype.Visible = False
  
End

Public Sub Coltype_Click()

  TypeC.text = Coltype.Item[0]
  Tlibelle.text = Coltype.Item[1]
  Coltype.visible = False
  Aff_Tarart()
  Coltype.clear
  Tart.SetFocus

End

Public Sub TypeC_man()

  Dim Typetab As Result

  If $choix = client Then
    Typetab = utils.db.Exec("SELECT cli_nom as libelle FROM Fiches_Cli WHERE cli_code=&1", TypeC.Text)
  Else
    Typetab = Utils.db.Exec("SELECT * FROM Fiches_Typec  where code = &1", TypeC.Text)
  Endif
  If Typetab.Available Then
    Tlibelle.Text = Typetab!libelle
    Aff_Tarart()
    Tart.SetFocus
  Else
    If Not Typetab.Available Then
      message.Warning("Cette enregistrement n'existe pas ! ")
      Typec.SetFocus
      Typec.Select
    Endif
  Endif

End

Public Sub Aff_Tarart()

  Dim f As ObjProp
  Dim Req As String
  Dim Res As Result
  Dim k As String
  Dim pxht, pxttc, rem, marge As String
  
  Col = New Collection
  pxht = " (art_prvt * coef) "
  pxttc = pxht & "*(1+(taux_tva/100)) "
  rem = "((art_pvht - " & pxht & ") / art_pvht)"
  marge = "((" & pxht & "- art_prvt)/ " & pxht & " )"
  If $choix = client Then
    Req = "SELECT art_code, art_design, art_prvt, art_nbd, art_prvt, art_pvht, art_tva, coef, ccli, " & pxht & " AS pxht, " & pxttc & " AS pxttc, " & rem & " AS remise, " & marge & " AS marge FROM Fiches_Tarcli LEFT JOIN Fiches_Art ON  art_code=cart JOIN Fiches_Tvaav ON code_tva = art_tva "
  Else
    Req = "SELECT art_code, art_design, art_prvt, art_nbd, art_prvt, art_pvht, art_tva, coef, type, " & pxht & " AS pxht, " & pxttc & " AS pxttc, " & rem & " AS remise, " & marge & " AS marge FROM Fiches_TarTypcli LEFT JOIN Fiches_Art ON  art_code=cart JOIN Fiches_Tvaav ON code_tva = art_tva " 
  Endif
  Res = Utils.db.Exec(Req & "LIMIT 1")
  If Not Res.Available Then Return
  k = Space(3) & "--- Code ---" & Space(3)
  f = New ObjProp(Res.Fields["art_code"], k)
'  f.AddChamp("Tooltip", "Appuyez sur Supr pour supprimer cette remise")
  Col.Add(f, k)
  k = Space(9) & "---------- Intitule ------------" & Space(9)
  f = New ObjProp(Res.Fields["art_design"], k)
  Col.Add(f, k)
  k = "- Px Rev. -"
  f = New ObjProp(Res.Fields["art_prvt"], k)
  f.AddChamp("Alignment", Align.Right, "0.000")
  Col.Add(f, k)
  k = "- Coeff. -"
  f = New ObjProp(Res.Fields["coef"], k)
  f.AddChamp("Alignment", Align.Right, "0.0000")
  Col.Add(f, k)
  k = "- Pv HT -"
  f = New ObjProp(Res.Fields["pxht"], k)
  f.AddChamp("Alignment", Align.Right, "0.000")
  Col.Add(f, k)
  k = "TVA"
  f = New ObjProp(Res.Fields["art_tva"], k)
  f.AddChamp("Alignment", Align.Center)
  Col.Add(f, k)
  k = " Pv TTC "
  f = New ObjProp(Res.Fields["pxttc"], k)
  f.AddChamp("Alignment", Align.Right, "0.000")
  Col.Add(f, k)
  k = " Px Public "
  f = New ObjProp(Res.Fields["art_pvht"], k)
  f.AddChamp("Alignment", Align.Right, "0.000")
  Col.Add(f, k)
  k = "Remise"
  f = New ObjProp(Res.Fields["remise"], k)
  f.AddChamp("Alignment", Align.Right, "0.00%")
  Col.Add(f, k)
  k = "  -Marge-  "
  f = New ObjProp(Res.Fields["marge"], k)
  f.AddChamp("Alignment", Align.Right, "0.00%")
  Col.Add(f, k)

  If trb Then trb.Close(Null)
  trb = New TriGen(Lstart, Col, Res, Req, 20, Me) As "ColTri"
  If $choix = client Then
     trb.ExtraSel(utils.db.Subst("ccli=&1", TypeC.Text), "AND")
  Else
    trb.ExtraSel(utils.db.Subst("type=&1", TypeC.Text), "AND")
  Endif
  
End

Public Sub ColTri_MajSel(val As Variant)

  Tart.Text = val
  If $choix = Client Then
    $resart = utils.db.Exec("SELECT art_code,art_design,art_nbd,art_prvt,art_pvht,art_coef, art_pvttc,art_tva, coef,ccli,art_cdarr,taux_tva FROM Fiches_Tarcli  JOIN Fiches_Art ON  art_code=cart JOIN Fiches_Tvaav ON art_tva=code_tva WHERE art_code=&1", Tart.Text)
  Else
  $resart = utils.db.Exec("SELECT art_code,art_design,art_nbd,art_prvt,art_pvht,art_coef, art_pvttc,art_tva, coef,type,art_cdarr,taux_tva FROM Fiches_TarTypcli  JOIN Fiches_Art ON  art_code=cart JOIN Fiches_Tvaav ON art_tva=code_tva WHERE art_code=&1", Tart.Text)
Endif
  If $resart.Available Then
    AffLig()  
  Endif
  Stop Event
 
End
'******************************************* On gère les produits *******************************************************************
'**********************************************************
'*                 Gestion onglet Saisie Articles         *
'**********************************************************

Public Sub Button5_click()

  SelectionArt()

End

Public Sub Tart_KeyPress()

  If Key.code = Key.F2 Then SelectionArt()
  If Key.code = Key.F4 Then Articles.ShowModal()

End

Public Sub Tart_LostFocus()

  Dim res As Result
  
  If Not Tart.Text Then Return
  If $choix = Client Then
    res = utils.db.Exec("SELECT * FROM Fiches_Tarcli WHERE ccli=&1 AND cart=&2", TypeC.Text, Tart.Text)
  Else
  res = utils.db.Exec("SELECT * FROM Fiches_TarTypcli WHERE type=&1 AND cart=&2", TypeC.Text, Tart.Text)
Endif
  If res.Available Then
   ColTri_MajSel(Tart.Text)
  Else
    Art_Man()
  Endif

End

Public Sub SelectionArt()

  Dim MyForm As TriArticles

  Bsel = False
  MyForm = New TriArticles(True, False, "art_code", "", "", Null, False, False, Me)
  MyForm.Showmodal()
  If Bsel = True Then
    Tart.text = Bval
    Tart_LostFocus()
    Tprht.SetFocus
  Else
    Tart.SetFocus
  Endif

End

'**************************************
'* On Selectionne l'article saisi     *
'**************************************
Public Sub Art_man()

  Dim Tab As String
Print "artman"
  If IsNull(Tart.Text) Then Return
  Tab = "Fiches_Art JOIN Fiches_Tvaav ON art_tva=code_tva"
  
  If Left$(Tart.Text, 1) = "*" Then
    $resart = Utils.db.Exec("SELECT * FROM " & Tab & " where art_code = &1", Mid$(Tart.Text, 2, Len(Tart.Text)))
  Else
    $resart = Utils.db.Exec("SELECT * FROM " & Tab & " where art_code = &1", Tart.Text)
  Endif
  If Not $resart.Available Then
    $resart = Utils.db.Exec("SELECT * FROM " & Tab & " where art_cequ = &1", Tart.Text)
  Endif
  If Not $resart.Available Then
    $resart = Utils.db.Exec("SELECT * FROM " & Tab & " where art_refcentrale = &1", Tart.Text)
  Endif
  If $resart.Available Then
    AffLig()
  Else
    If start.son Then
      Music.Play
    Endif
    Try message.Warning(" L'article " & Tart.Text & " n'existe pas ! ")
    Tart.Clear
    Tart.SetFocus
  Endif

End

Private Sub AffLig()
  
  Tart.text = $resart!art_code
  Tdesign.text = $resart!art_design
  Tprht.Value = $resart!art_prvt
  Tprht.nbdec = $resart!art_nbd
  Tpvht.nbdec = $resart!art_nbd
  TarTva.Text = $resart!art_tva
  pvpublic.Value = $resart!art_pvht
  pvpublic.nbdec = $resart!art_nbd
  TpvTTc.nbdec = $resart!art_nbd
  If $resart.Fields.Exist("coef") Then Tcoeff.Value = $resart!coef Else tcoeff.Value = $resart!art_coef
  Nbd.Text = $resart!art_nbd
  
  Tcoeff_Lostfocus()
  
End

'********************************************
'*           On calcule la remise           *
'********************************************
Public Sub Calc_Remise() As Float

  Dim rrr As Float
  'La remise est calculée a partir du pv ht public
  If pvpublic.Value <> 0 Then rrr = ((pvpublic.Value - Tpvht.Value) / pvpublic.Value) * 100 Else rrr = 0
  Return rrr
  
End

Public Sub Button2_Click()

  Me.close

End

Public Sub Button3_Click()


  If Message.Delete("Attention ! Vous allez supprimer TOUS les produits pour cette séléction.", "Oui", "Non") = 1 Then
    If $choix = client Then 
      Utils.db.Exec("DELETE FROM Fiches_Tarcli WHERE ccli = &1", TypeC.text)
    Else
      Utils.db.Exec("DELETE FROM Fiches_TarTypcli WHERE type = &1", TypeC.text)
    Endif
    Refresh()
    Aff_Tarart
  Endif

End

Public Sub Button6_Click()

  Dim Taresult As Result
  Dim tab As String
  Dim code As String

  If IsNull(TypeC.text) Or IsNull(Tart.Text) Then 
    Message.Warning("Veuillez vérifier votre saisie SVP !")
    TypeC.setfocus
    Return
  Endif

  If $choix = client Then
    tab = "Fiches_Tarcli"
    code = "ccli"
  Else
    tab = "Fiches_TarTypcli"
    code = "type"
  Endif
  utils.db.Begin
  Taresult = utils.db.Edit(tab, code & " = &1 AND cart=&2", TypeC.Text, Tart.Text)
  If Not Taresult.Available Then
    Taresult = utils.db.Create(tab)
  Endif
  Taresult[code] = TypeC.Text
  Taresult!cart = Tart.Text
  Taresult!coef = tcoeff.Value
  Taresult.Update
  utils.db.Commit
  Aff_Tarart()
  Refresh()
  Tart.SetFocus()
  
End

Public Sub Refresh()

  Tprht.Clear
  Tcoeff.Clear
  Tpvht.Clear
  TarTva.Text = ""
  Tpvttc.Clear

End

Public Sub Button6_KeyPress()

  If Key.code = Key.Enter Or Key.code = Key.Return Then Button6_Click()

End

Public Sub button6_GotFocus()

  remise.Value = Calc_Remise()

End

Public Sub Tcoeff_Lostfocus()

  Dim ltx As LTaxe
  
  If IsNull($resart) Or If Not $resart.Available Then Return
  $lpx = New LPrix(Tprht.Value, 1, $resart!art_cdarr) As "LPrix"
  ltx = New LTaxe(Tprht.Value * (1 - tcoeff.Value), LTaxe.Remise)
  Object.Lock($lpx)
  $lpx.Add_Taxe(ltx)
  Object.Unlock($lpx)
  ltx = New LTaxe($resart!taux_tva & "%", LTaxe.TVA)
  $lpx.Add_Taxe(ltx) 

End

Public Sub ColTri_KeyPress()

  If Key.Code = Key.Delete Then
    If IsNull(TypeC.Text) Or IsNull(Tart.Text) Then Return
      If message.Question("Attention ! Vous allez supprimer la remise du produit " & Tart.Text, "Oui", "Non") = 1 Then
        If $choix = client Then
          Utils.db.Exec("DELETE FROM Fiches_Tarcli WHERE ccli = &1 and cart = &2", TypeC.text, Tart.Text)
        Else
          Utils.db.Exec("DELETE FROM Fiches_Tarcli WHERE type = &1 and cart = &2", TypeC.text, Tart.Text)
      Endif
      Refresh
      Aff_Tarart
    Endif
  Endif

End

'***********************************
'*  On gère le prix de vente ht    *
'***********************************
Public Sub Tpvht_LostFocus()

  Dim ltx As LTaxe
  
  If IsNull($resart) Then Return
  $lpx = New LPrix(Tpvht.Value, 1, $resart!art_cdarr) As "LPrix"
  ltx = New LTaxe($resart!taux_tva & "%", LTaxe.TVA)
  $lpx.Add_Taxe(ltx)

End

Public Sub Remise_LostFocus()

  Dim ltx As LTaxe
  
  If IsNull($resart) Then Return
  $lpx = New LPrix(pvpublic.Value, 1, $resart!art_cdarr) As "LPrix"
  ltx = New LTaxe(Remise.Text & "%", LTaxe.Remise)
  Object.Lock($lpx)
  $lpx.Add_Taxe(ltx)
  ltx = New LTaxe($resart!taux_tva & "%", LTaxe.TVA)
  Object.Unlock($lpx)
  $lpx.Add_Taxe(ltx)

End
'***********************************
'* On gère le prix de vente TTC    *
'***********************************
Public Sub TpvTTc_LostFocus()

  Dim ltx As LTaxe
  
  If IsNull($resart) Then Return
  If $ttc <> TpvTTc.Value Then
    Tpvht.Value = Tpvttc.Value / (1 + ($resart!taux_tva / 100))
    Tpvht_LostFocus()
  Endif
  
End

'***************************************
'*      On calcule le coefficient      *
'***************************************
Public Sub Calc_coeff() As Float

  If Tprht.Value = 0 Then Return 0
  Return Tpvht.Value / Tprht.Value

End

Public Sub LPrix_change(value As LPrix)

  Tpvht.Value = $lpx.PuNHT.G
  Tpvttc.Value = $lpx.PuNTC.G
  remise.Value = Calc_Remise()
  Tcoeff.Value = Calc_coeff()
  $ttc = TpvTTc.Value
  If Tpvht.Value < Tprht.Value Then
    If start.son Then
      Music.Play
    Endif
    Try message.warning("ATTENTION ! Le Prix de vente est inférieur au prix de revient", "OK")
  Endif

End
'********************
'* On lance la doc  *
'********************
Public Sub Button1_Click()

  Doc.Open("TarifClient")

End

' Gambas class file

Public Enum sans_lcr, lcr_nacc, lcr_acc
Public Enum sans_caisse, espece, carte, cheque, bon_achat, cheque_cadeaux, autre, cheque_entreprise, bonus, ticket_resto
Public RegCais As String[] = ["Sans", "Espéce", "Carte", "Chèque", "Bon d'achat", "Chèque cadeaux", "Autres", "Chèque entreprise", "Bonus", "Tickets restaurant"]

Private $ressel As Result
Private $res As Result
Private $bars As Barselection

Public Sub _new()

  Dim i As Integer
  Dim ch As Chainage

  ch = New Chainage([code, libel, jour])
  Utils.Observers(Me)
  $bars = New Barselection("001110010", sel) As "bar1"
  GridView1.Columns.Count = 3
  GridView1.Columns[0].Text = "Code"
  GridView1.Columns[0].Alignment = Align.Center
  GridView1.Columns[0].Width = 70
  GridView1.Columns[1].Text = "Libéllé"
  GridView1.Columns[1].Alignment = Align.Center
  GridView1.Columns[1].Expand = True
  GridView1.Columns[2].Text = "Journal"
  GridView1.Columns[2].Alignment = Align.Center
  GridView1.Columns[2].Width = 70
  
  code.MaxLenght = 2
  libel.MaxLenght = 20
  jour.MaxLenght = 2
  cptint.reg = "[0-9]{0,9}"
  $ressel = Utils.db.Exec("SELECT * FROM Fiches_Reglements")
  If $ressel.Available Then
    GridView1.rows.Count = $ressel.Count
  Endif
  For i = 0 To RegCais.Max
    Combocais.Add(RegCais[i], i)
  Next
  Combocais.Tag = 0
  decaissement.Enabled = Start.LocalSettings["/Soc" & Start.Societe & "/Decais"] 

End

Private Sub affiche()
  
  Dim res As Result
  
  If Not Object.IsValid($ressel) Then Return
  If Not $ressel.Available Then Return
 
  jour.Text = $ressel!journal
  If $ressel!cptint Then interne.Value = True Else interne.Value = False
  cptint.Text = $ressel!cptint
  code.Text = $ressel!code
  libel.Text = $ressel!libel
  Combocais.Tag = $ressel!caisse
  regr.Value = $ressel!regroup
  Combocais.Text = RegCais[$ressel!caisse]
  Select Case $ressel!lcr
    Case sans_lcr
      sanslcr.Value = True
    Case lcr_acc
      lcra.Value = True
    Case lcr_nacc
      lcr.Value = True
  End Select
  decaissement.Value = $ressel!decaissement
  borderau.Value = $ressel!bordereaux
  
  res = Utils.db.Exec("SELECT * FROM Fiches_Journaux WHERE code_jo=&1", jour.Text)
  If res.Available Then libjo.Text = res!libelle_jo Else libjo.Text = ""
  res = Utils.db.Exec("SELECT * FROM Fiches_Comptes WHERE compte_cc=&1", cptint.Text)
  If res.Available Then libcpt.Text = res!intitule_cc Else libcpt.Text = ""
  
End

'radio button
Public Sub interne_Click()

  If interne.Value Then
    pcint.Enabled = True
    cptint.SetFocus
  Else
    pcint.Enabled = False
    cptint.Clear
    borderau.Value = False
  Endif

End

Public Sub rdb_click()

  pcint.Enabled = False

End
'gestion des boutons de selection

Public Sub bar1_creer()
  
  code.Enabled = True
  libel.Enabled = True
  code.SetFocus
  Utils.db.Begin
  $res = Utils.db.Create("Fiches_Reglements")
  
End

Public Sub bar1_modif()
  
  Utils.db.Begin
  $ressel.MoveTo(GridView1.Row)
  $res = Utils.db.Edit("Fiches_Reglements", "code=&1", $ressel!code)
  code.Enabled = False
  libel.Enabled = True
  
End

Public Sub bar1_annule(creer As Boolean, anul As Boolean)
  
  Utils.db.Rollback
  code.Enabled = False
  libel.Enabled = False
  
End

Public Sub bar1_valide(creer As Boolean, anul As Boolean)
  
  Dim i As Integer
  
  i = $ressel.Index
  If Not code.Text Then
    Message.Error("Code obligatoire")
    Utils.db.Rollback
    code.SetFocus
    Return
  Endif

  If interne.Value Then
    If Not cptint.Text Then
      Message.Error("compte interne obligatoire")
      Utils.db.Rollback
      cptint.SetFocus
      Return
    Else
      $res!cptint = cptint.Text
    Endif
  Endif

  If Not jour.Text Then
    Message.Error("N° de journal obligatoire")
    jour.SetFocus
    Utils.db.Rollback
    Return
  Endif
  $res!lcr = sans_lcr
  If lcr.Value Then $res!lcr = lcr_nacc
  If lcra.Value Then $res!lcr = lcr_nacc
  If code.Enabled Then $res!code = code.Text
  $res!libel = libel.Text
  $res!journal = jour.Text
  $res!decaissement = decaissement.Value
  $res!bordereaux = Borderau.Value
  $res!caisse = Combocais.Index
  $res!regroup = regr.Value
  $res.Update
  Utils.db.Commit
  
  $ressel = Utils.db.Exec("SELECT * FROM Fiches_Reglements")
  $ressel.MoveTo(i)
  GridView1.Rows.Count = $ressel.Count
  code.Enabled = False
  libel.Enabled = False
  
End

Public Sub bar1_suprimer()

  Utils.db.Delete("Fiches_Reglements", "code=&1", $ressel!code)
  $ressel = Utils.db.Exec("SELECT * FROM Fiches_Reglements")
  affiche

End

Public Sub bar1_quitter()

  Me.Close

End
'lostfocus
Public Sub jour_LostFocus()

  Dim res As Result
  
  If jour.Text Then
    res = Utils.db.Exec("SELECT * FROM Fiches_Journaux WHERE code_jo=&1 AND type_jo='TR'", jour.Text)
    If res.Available Then 
      libjo.Text = res!libelle_jo
    Else
      Message.Error("journal innexistant")
      jour.SetFocus
    Endif
  Else
    libjo.Text = ""
  Endif

End

Public Sub cptint_LostFocus()

  Dim res As Result
  
  If cptint.Text Then
    res = Utils.db.Exec("SELECT * FROM Fiches_Comptes WHERE compte_cc=&1", cptint.Text)
    If res.Available Then 
     libcpt.Text = res!intitule_cc
    Else 
      Message.Error("Compte innexistant")
      cptint.SetFocus
    Endif
  Else
    libcpt.Text = ""
  Endif
  
End

Public Sub Combocais_Click()

  Dim res As Result
  
    If Combocais.Index <> Combocais.Tag And Combocais.Index <> 0 Then
    res = Utils.db.Exec("SELECT * FROM Fiches_Reglements WHERE caisse=&1", Combocais.Index)
    If res.Available Then
      Message.Warning("Liaison déjà définie")
      Combocais.Text = RegCais[sans_caisse]
    Endif
  Endif

End
'recherche
Public Sub Butint_Click()

  Dim MyForm As TriComptes
  Dim tri As String

  Tri = "cast(compte_cc AS char)"
  MyForm = New TriComptes(Tri, "B", "", "")
  MyForm.Showmodal()
  If Comptabilite.Bsel = True Then
    cptint.Text = Comptabilite.sCmpt
    libcpt.Text = Comptabilite.sDesg
  Else
    libcpt.Text = ""
  Endif
  cptint.SetFocus

End

Public Sub bar1_recherche()
  
  Dim i As Integer
  
  If Not Object.IsValid($ressel) Then Return
  If Not $ressel.Available Then Return
  If recherche.Tag Then
    If recherche.Tag <> 1 Then Return
    recherche.Tag = ""
    recherche.Clear
    recherche.Visible = False
  Else
    recherche.Tag = 1
    recherche.Visible = True
    recherche.Raise
    i = $ressel.Index
    $ressel.MoveFirst
    Repeat
      recherche.Add($ressel!code, $ressel!libel)
    Until $ressel.MoveNext()
  Endif
  $ressel.MoveTo(i)
End

Public Sub butjour_Click()

  Dim res As Result
  
  If Not Object.IsValid($ressel) Then Return
  If Not $ressel.Available Then Return
  If recherche.Tag Then
    If recherche.Tag <> 2 Then Return
    recherche.Tag = ""
    recherche.Clear
    recherche.Visible = False
  Else
    recherche.Tag = 2
    recherche.Visible = True
    recherche.Raise
    res = Utils.db.Exec("SELECT * FROM Fiches_Journaux WHERE type_jo='TR'")
    res.MoveFirst
    Repeat
      recherche.Add(res!code_jo, res!code_jo & " " & res!libelle_jo)
    Until res.MoveNext()
  Endif

End
'gestion listeview
Public Sub recherche_Click()

  Select Case Last.Tag
    Case 1
      $ressel.MoveFirst()
      While recherche.Current.Key <> $ressel!code 
        $ressel.MoveNext()
      Wend
      affiche
    Case 2
      jour.Text = recherche.Current.Key
      libjo.Text = recherche.Current.Text
  End Select
  recherche.Tag = ""
  recherche.Clear
  recherche.Visible = False
 
End

Public Sub recherche_KeyPress()

  If Key.Code = Key.Esc Then
    recherche.Tag = ""
    recherche.Clear
    recherche.Visible = False
  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer)

  $ressel.MoveTo(row)
  Select Case Column
    Case 0
       GridView1.Data.Text = $ressel!code
    Case 1
       GridView1.Data.Text = $ressel!libel
    Case 2
       GridView1.Data.Text = $ressel!journal
  End Select
 
End

Public Sub GridView1_Click()

  $ressel.MoveTo(GridView1.Row)
  affiche()

End


Public Sub borderau_Click()

  If borderau.Value Then
    cptint.SetFocus
  Else
    If Not cptint.Text Then cptint.SetFocus
  Endif

End

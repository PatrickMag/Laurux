' Gambas class file

'----------------------------------------------------------------------------
'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'################################################
'# nom du fichier           : Facturefm.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 01/09/2007
'# Facturation fin de mois
'################################################
'

son As Integer = Start.Son
b As Integer
'Tab As String
'Tab2 As String
rResult As Result
CblRslt As Result
numbl As Result
Nfac As String
Jnl As String
cpt As String
Intit As String
numecr As String
numr As Integer
numrd As Integer
numr2 As Integer ' numero ecriture définitif
cptRem As String
Ctva As String
Ctx As String
cpttva As String
IntitTva As String
Txtleg As String
Txtleg2 As String
numeroFac As String
Remmo As String
Remart As String
Mgart As String ' Montant marge article
Mgmo As String ' Montant marge mo
Ttva As Float
dateech As String
Filename As String
jour As String
Nbl As Integer
Nlgn As String
Totalmo As String
Totalart As String
Totalrem As String
totalht As String
Totht As String
typeL As String
Fam As String
Ardi As String
nbdec As String
Totttc As String
Brutht As String
Rem As String
TotEuros As String = "0"
nom As String
Mttc As String
adr211 As String
adr222 As String
numbonl As String
intituleb As String
Bonl As Result
dtex As String
dte As String
Devises As String
PosY As Integer
Visudoc As Boolean
Coulfond As New String[]
Private ty As String
Private scourriel As String
Private spiece As String
Private sclient As String
Private nmclient As String
Private sclient2 As String
Public Bsel As Boolean = False
Public Cclient As String
Private Ccli As String
Private dateech2 As String
Private pdf As Object
Private Rs2 As String
Private Vic As String
Private Vichq As String
Private Viautre As String
Private Cptreglt As String
Private Reglmt As String
Private Prxdec As Boolean = Start.LocalSettings["/Soc" & Start.Societe & "/Prxdec"]
Private Codecli As String
Private NomCli As String
Private adr1 As String
Private adr2 As String
Private cpcli As String
Private villecli As String
Private bnumfac As Boolean = False
Private Liste As Boolean = False
Private $soption As String
Private $sreq As String
Private $obs As Observer
Private frais As String = "0"
Private $bl As String = "Fiches_Bl"
Private $process As Boolean = False
Private $ligbl As String = "Fiches_Ligbl"
Private $fac As String = "Fiches_HistoFac"
Private $ligfac As String = "Fiches_HistoLigfac"

'**********************************************************
'*initialisation écrans et gestion onglet client          *
'**********************************************************
Public Sub _New()

  Dim res As Result
  Dim Dppc As Date
  Dim Tab As String = "Fiches_Parametres"
  Dim $lp, $imp As String

  $soption = utils.Option()
  rResult = Utils.db.Exec("SELECT * FROM " & tab & "")
  If rResult.Available Then
    devises = rResult!devise
    If IsNull(devises) Then devises = "Euros"
  Endif
  Coulfond = Utils.Coulfd()
  Utils.Observers(Me)
  Music.Load(Start.Musique)

  Me.Center

  Colbl.Columns.count = 6
  Colbl.Columns[0].Width = 80
  Colbl.Columns[1].Width = 150
  Colbl.Columns[2].Width = 60
  Colbl.Columns[3].Width = 120
  Colbl.Columns[4].Width = 100
  Colbl.Columns[0].Text = "Code"
  Colbl.Columns[0].Alignment = 1
  Colbl.Columns[1].Text = "Nom"
  Colbl.Columns[1].Alignment = 1
  Colbl.Columns[2].Text = "Cp"
  Colbl.Columns[2].Alignment = 1
  Colbl.Columns[3].Text = "Ville"
  Colbl.Columns[4].Text = "Total ht"
  Colbl.Columns[4].Alignment = 2
  Colbl.Columns[5].Text = "Total ttc"
  Colbl.Columns[5].Alignment = 2

  Coldetail.Columns.count = 4
  Coldetail.Columns[0].Width = 130
  Coldetail.Columns[1].Alignment = 3
  Coldetail.Columns[2].Alignment = Align.Left
  Coldetail.Columns[1].Width = 150
  Coldetail.Columns[2].Width = 120
  Coldetail.Columns[3].Width = 120
  Coldetail.Columns[0].Text = "N° BL"
  Coldetail.Columns[1].Text = "Date BL"
  Coldetail.Columns[2].Text = "montant HT"
  Coldetail.Columns[2].Alignment = 2
  Coldetail.Columns[3].Text = "montant TTC"
  Coldetail.Columns[3].Alignment = 2

  ' On calcule le dernier jour du mois en cours.
  datej.Text = Format$(Now, "dd.mm.yyyy")
  Dppc = Date(Right$(datej.Text, 4), Mid$(datej.Text, 4, 2), Left$(datej.Text, 2))
  If Month(Dppc) < 12 Then
    datej.Text = Day(Date(Year(dppc), Month(dppc) + 1, 1) - 1) & "." & Mid$(datej.Text, 4, 2) & "." & Right$(datej.Text, 4)
  Else
    datej.Text = Day(Date(Year(dppc) + 1, 1, 1) - 1) & "." & Mid$(datej.Text, 4, 2) & "." & Right$(datej.Text, 4)
  Endif
  datej.SelectAll
  Datej.SetFocus

  If $soption = "B" Or $soption = "T" Then Panel2.Visible = True
  combregr.Add("Sans")
  combregr.Add("Décade")
  combregr.Add("Quinzaine")
  combregr.Add("Mensuel")
  res = utils.db.Exec("SELECT * FROM Fiches_Typec")
  Combocli.Add("Tous")
  If res.Available Then
    res.MoveFirst
    Repeat
      Combocli.Add(res!code & " - " & res!libelle)
    Until res.MoveNext()
  Endif
  res = utils.db.Exec("SELECT * FROM Fiches_Commerciaux")
  Combocom.Add("Tous")
  If res.Available Then
    Repeat
      Combocom.Add(res!code & " - " & res!nom)
    Until res.MoveNext()
  Endif
  Combotyp.Add("Toutes")
  Combotyp.Add("Boissons")
  Combotyp.Add("Autres")
  'gestion des dates
  $obs = New Observer(datej) As "dte"
  $obs = New Observer(datei) As "dte"

End

Public Sub Form_Close()

  If $process Then
    If Message.Warning("Il y a une édition en cours : Fermer le programme peut provoquer des dommages sur la base de données \n Voulez vous fermer quand même ?", "Oui", "Non") = 2 Then
      Stop Event
    Else
      If Exist(filename) Then Try Kill filename
    Endif
  Endif
  
End
'**************************
'*  On récupère les BL    *
'**************************
Public Sub recup_bl()

  Dim Tab3 As String
  Dim Tab4 As String
  Dim Client As Result
  Dim Bl As Result
  Dim totf As Result
  Dim Total, totalttc As String
  Dim dteN, dateinf As String
  Dim i As Integer
  Dim orientation As Boolean
  Dim gest As Boolean
  Dim ht, ttc, montfrais As Float
  
  Tab3 = "Facturefm"
  Tab4 = "Bl"
  Total = "0"
  Utils.db.Exec("drop Table IF exists " & Tab3 & "")
  Utils.db.Exec("CREATE TABLE " & Tab3 &
    "(code Char(8) NOT NULL," &
    "nom Char(35)," &
    "cp Char(5)," &
    "adr1 Char(35)," &
    "adr2 Char(35)," &
    "ville Char(35)," &
    "totf Char(12)," &
    "tottc Char(12)," &
    "gestbl Decimal(6,2)," &
    "PRIMARY KEY (code, nom)) " & Utils.db.Engine())
  Utils.db.Exec("drop Table IF exists " & Tab4 & "")
  Utils.db.Exec("CREATE TABLE " & Tab4 &
    "(num Char(12) NOT NULL," &
    "code Char(10)," &
    "nom Char(35)," &
    "date date," &
    "ht Char(12)," &
    "ttc Char(12)," &
    "PRIMARY KEY (num)) " & Utils.db.Engine())
  dteN = Right$(datej.Text, 4) & Mid$(datej.Text, 4, 2) & Left$(datej.Text, 2)
  If Radiobutton1.Value = True Then
    codeclient.Background = &HD4D0C8& '&HF9F9BD&
    Client = Utils.db.Exec("SELECT * FROM " & $bl & " Left join " & Cbase.Table("TabCli") & " on cli_code = cdclibl where (type = &1 or type=&2) and totalht <> &3 and  cli_div = &5 and imp <> &4 and cli_div = &5" & $sreq & " order by cdclibl, numbl", "B", "F", 0, 1, Divers.value)
  Else
    Client = Utils.db.Exec("SELECT * FROM " & $bl & " Left join " & Cbase.Table("TabCli") & " on cli_code = cdclibl where (cdclibl = &1 and type = &2 and totalht <> &4 and datebl <= &5 and cli_div = &7)  or (cdclibl = &1 and type = &3 and totalht <> &4 and datebl <= &5 and imp <> &6 and cli_div = &7)  order by cdclibl, numbl", codeclient.Text, "B", "F", 0, dteN, 1, Divers.value)
  Endif
  If Client.Available Then
    Codecli = Client!cdclibl
    Nomcli = Client!nmclibl
    adr1 = Client!adr1bl
    adr2 = Client!adr2bl
    cpcli = Client!cpbl
    villecli = Client!villebl
    Repeat
      If Divers.value And Codecli <> Client!cdclibl Or If Divers.value And Nomcli <> Client!nmclibl Then
        Nomcli = Client!nmclibl
        adr1 = Client!adr1bl
        adr2 = Client!adr2bl
        cpcli = Client!cpbl
        villecli = Client!villebl
        codecli = Client!cdclibl
        gest = False
        montfrais = 0
      Endif
      If Codecli <> Client!cdclibl Then
        gest = False
        montfrais = 0
        Codecli = Client!cdclibl
      Endif
      If Not Divers.value Then
        totf = Utils.db.Exec("SELECT * FROM " & Tab3 & " WHERE code = &1", Client!cdclibl)
      Else
        totf = Utils.db.Exec("SELECT * FROM " & Tab3 & " WHERE code = &1 and nom = &2", Client!cdclibl, Nomcli)
      Endif
      If totf.Available Then
        Total = Format$(Val(totf!totf) + Val(Utils.cpoint(Client!totalht)), "0.00")
        Totalttc = Format$(Val(totf!tottc) + Val(Utils.cpoint(Client!totalttc)), "0.00")
        ht = Val(Utils.cpoint(Client!totalht))
        ttc = Val(Utils.cpoint(Client!totalttc))
        If Utils.totobs([client!gestbl]) <> 0 Then  'Frais de facture : on empeche les frais de se cumuler => seul le 1° sera pris en compte.
          If gest Then
            Total = Format$(Val(Total) - client!gestbl, "0.00")
            Totalttc = Format$(Val(totalttc) - totfrais(client!gestbl), "0.00")
            ht -= client!gestbl
            ttc -= totfrais(client!gestbl)
          Else
            gest = True
            montfrais = client!gestbl
          Endif
        Endif
        If Not Divers.value Then
          Utils.db.Exec("UPdate " & Tab3 & " SET totf = &1, tottc = &2, gestbl=&4 WHERE code = &3", Total, Totalttc, Client!cdclibl, montfrais)
        Else
          Utils.db.Exec("UPdate " & Tab3 & " SET totf = &1, tottc = &2, gestbl=&5 WHERE code = &3 and nom = &4", Total, Totalttc, Client!cdclibl, Nomcli, montfrais)
        Endif
      Else
        
        Try Total = Format$(Client!totalht, "0.00")
        Try Totalttc = Format$(Client!totalttc, "0.00")
        ht = Client!totalht
        ttc = Client!totalttc
        If Utils.totobs([client!gestbl]) <> 0 Then 
          gest = True
          montfrais = client!gestbl
        Endif
        If Not Divers.value Then
          Utils.db.Exec("INSERT INTO " & Tab3 & " (code, nom, cp, ville, totf, tottc,gestbl) Values (&1, &2, &3,&4,&5, &6, &7)", Client!cdclibl, Client!nmclibl, Client!cpbl, Client!villebl, Total, totalttc, montfrais)
        Else
          Utils.db.Exec("INSERT INTO " & Tab3 & " (code, nom, adr1, adr2, cp, ville, totf, tottc, gestbl) Values (&1, &2, &3,&4,&5, &6, &7, &8, &9)", Client!cdclibl, Nomcli, adr1, adr2, cpcli, villecli, Total, totalttc, montfrais)
        Endif
      Endif
      Total = 0
      Bl = Utils.db.Exec("SELECT * FROM " & $ligbl & " where num_ligbl = &1 order by num_ligbl", Client!numbl)
      If Bl.Available Then
        Utils.db.Exec("INSERT INTO " & Tab4 & " (num, code, nom, date, ht, ttc) Values (&1, &2, &3, &4, &5, &6)", Client!numbl, Client!cdclibl, Nomcli, Client!datebl, Format(ht, "0.00"), Format(ttc, "0.00"))
      Endif
    Until Client.MoveNext()
  Endif
  Client = Utils.db.Exec("SELECT * FROM " & Tab3 & " order by nom")
  If Client.Available Then
    Repeat
      Colbl.Add(Client!code & Client!nom, Client!code & Client!nom)
      Colbl.Item[0] = Client!code
      Colbl.Item[1] = Client!nom
      Colbl.Item[2] = Client!cp
      Colbl.Item[3] = Client!ville
      Colbl.Item[4] = Client!totf
      Colbl.Item[5] = Client!tottc
    Until Client.MoveNext()
    client.MoveFirst
  Endif
  'choix du type de facture à imprimer
  If Combotyp.Current.Text <> "Toutes" And Client.Available Then
    Colbl.MoveFirst
    Repeat
      orientation = utils.orientation(Colbl.Item[0], "FM")
      If (orientation And Combotyp.Current.Text = "Autres") Or (Not orientation And Combotyp.Current.Text = "Boissons") Then
        supclient(Colbl.Item[0], Colbl.Item[1])
      Endif
    Until Colbl.MoveNext()
    affclient
  Endif

End

Private Function totfrais(frs As Float) As Float
  
  Dim res As Result
  
  res = Utils.db.Exec("SELECT tvafrais,taux_tva  FROM Fiches_Parametres,Fiches_Tvaav WHERE tvafrais=code_tva")
  If res.Available Then
    frs = frs * (1 + (res!taux_tva / 100))
    Return frs
  Endif
  
End

Public Sub Colbl_Click()

  Dim Client As Result
  Dim Tab4 As String = "Bl"

  Coldetail.clear
  With Utils
    If Not Colbl.Available Then
      Colbl.clear
    Else
      If Divers.value Then
        Client = Utils.db.Exec("SELECT * FROM " & Tab4 & " where code = &1 and nom = &5 order by num", Colbl.Item[0], "B", "F", 0, Colbl.Item[1])
      Else
        Client = Utils.db.Exec("SELECT * FROM " & Tab4 & " where code = &1 order by num", Colbl.Item[0], "B", "F", 0)
      Endif
      sclient = Colbl.Item[0]
      nmclient = Colbl.Item[1]
      If Client.Available Then
        Repeat
          If Divers.value Then
            Coldetail.Add(Client!num & Client!nom, Client!num & Client!nom)
          Else
            Coldetail.Add(Client!num, Client!num)
          Endif
          Coldetail.Item[0] = Client!num
          Coldetail.Item[1] = .Cdate_Aff(Client!date)
          Coldetail.Item[2] = Format$(Val(.cpoint(Client!ht)), "0.00")
          Coldetail.Item[3] = Format$(Val(.cpoint(Client!ttc)), "0.00")
        Until Client.MoveNext()
      Endif
    Endif
  End With

End

Public Sub Colbl_Keypress()

  If Key.code = Key.Delete And Colbl.Count <> 0 Then
    supclient(sclient, nmclient)
    affclient
  Endif

End

Public Sub supclient(sclient As String, nmclient As String)

  Dim Client As Result
  Dim Tab3 As String = "Facturefm"
  Dim Tab4 As String = "Bl"

  If Divers.value Then
    Utils.db.Exec("delete from " & Tab3 & " WHERE code = &1 and nom = &2", sclient, nmclient)
    Utils.db.Exec("delete from " & Tab4 & " WHERE code = &1 and nom = &2", sclient, nmclient)
  Else
    Utils.db.Exec("delete from " & Tab3 & " WHERE code = &1 ", sclient)
    Utils.db.Exec("delete from " & Tab4 & " WHERE code = &1 ", sclient)
  Endif

End

Public Sub affclient()

  Dim client As Result

  Colbl.Clear
  Coldetail.Clear
  Client = Utils.db.Exec("SELECT * FROM Facturefm order by nom")
  If Client.Available Then
    Repeat
      If Divers.value Then
        Colbl.Add(Client!code & Client!nom, Client!code & Client!nom)
      Else
        Colbl.Add(Client!code, Client!code)
      Endif
      Colbl.Item[0] = Client!code
      Colbl.Item[1] = Client!nom
      Colbl.Item[2] = Client!cp
      Colbl.Item[3] = Client!ville
      Colbl.Item[4] = Client!totf
      Colbl.Item[5] = Client!tottc
    Until Client.MoveNext()
  Endif

End

Public Sub Coldetail_Click()

  sclient2 = Coldetail.Item[0]

End

Public Sub Coldetail_Activate()

  Dim MyForm As AFacture

  MyForm = New AFacture(Coldetail.Item[0], sclient, nmclient, Coldetail.Item[1])
  MyForm.Showmodal()

End

Public Sub Coldetail_Keypress()

  Dim Client As Result
  Dim Totalht, totalttc As String = "0"
  Dim Tab4 As String = "Bl"
  Dim cle As String = Colbl.Key

  With Utils
    If Key.code = Key.Delete And Coldetail.Count <> 0 Then
      Utils.db.Exec("delete from " & Tab4 & " WHERE num = &1 ", sclient2)
    Endif
    If Divers.value Then
      Client = Utils.db.Exec("SELECT * FROM " & Tab4 & " where code = &1 and nom = &2 order by num", sclient, nmclient)
    Else
      Client = Utils.db.Exec("SELECT * FROM " & Tab4 & " where code = &1 order by num", sclient)
    Endif
    Coldetail.Clear
    If Client.Available Then
      Repeat
        Coldetail.Add(Client!num, Client!num)
        Coldetail.Item[0] = Client!num
        Coldetail.Item[1] = .Cdate_Aff(Client!date)
        Coldetail.Item[2] = Format$(Val(.cpoint(Client!ht)), "0.00")
        Coldetail.Item[3] = Format$(Val(.cpoint(Client!ttc)), "0.00")
        Totalht = Format$(Val(.cpoint(Totalht)) + Val(.cpoint(Client!ht)), "0.00")
        Totalttc = Format$(Val(.cpoint(Totalttc)) + Val(.cpoint(Client!ttc)), "0.00")
      Until Client.MoveNext()
    Endif
    Colbl.MoveFirst()
    Repeat
      Colbl.Item.Selected = True
      If Colbl.key = cle Then
        'If Colbl.Item[0] = sclient Then
        Colbl.Item[4] = Totalht
        Colbl.Item[5] = Totalttc
        Colbl_Click()
        Break
      Endif
    Until Colbl.MoveNext()

  End With

End

'**********************************************************
'*  On controle si la date saisie est dans les bornes     *
'**********************************************************

Public Sub Quittedate(datej As TextBox)

  Dim dte2 As String
  Dim dte3 As String
  Dim ye As String
  Dim Tab As String = "Fiches_Parametres"

  rResult = Utils.db.Exec("SELECT * FROM " & Tab & "")
  dtex = rResult!dteclec ' On recupere la date de fin d'exercice
  dte = rResult!dtepc
  dte = Utils.Cdate_aff(dte)
  dte3 = Right$(dte, 4) & Left$(dte, 2)
  dte2 = rResult!dteclec1
  dte2 = Utils.Cdate_aff(dte2)
  ye = Right$(dte2, 4)
  Ye = Val(Ye) + 1
  With utils
    datej.text = .Cdate_calc(datej.text)
    datej.Text = .Cdate_aff(datej.Text)
    b = 0
    If datej.Text = "00.00.0:00" Then
      datej.Text = Format$(Now, "dd.mm.yyyy")
      b = 1
      datej.SetFocus
    Else
      dte3 = rResult!dtepp
      dte3 = Right$(dte3, 4) & Left$(dte3, 2)
      If Right$(datej.text, 4) & Mid$(datej.text, 4, 2) < dte3 Then
        If son Then
          Music.Play
        Endif
        If Message.Warning("Attention ! La date saisie est inférieure au début de la période en cours.", "Ok") = 1 Then
          datej.SetFocus
          B = 1
        Endif
      Else
        dte3 = rResult!dtepec
        If Left$(dte3, 2) = "12" Then
          Mid(dte3, 4, 4) = Str(Val(Right(dte3, 4)) + 1)
          Mid(dte3, 1, 2) = "01"
        Else
          Mid(dte3, 1, 2) = Format(Val(Left(dte3, 2)) + 1, "00")
        Endif
        dte3 = Right$(dte3, 4) & Left$(dte3, 2)
        If Right$(datej.text, 4) & Mid$(datej.text, 4, 2) > dte3 Then
          If son Then
            Music.Play
          Endif
          If Message.Warning("Attention ! La date saisie est supérieure à la période en cours.", "Ok") = 1 Then
            datej.SetFocus
            B = 1
          Endif
        Else
        Endif
      Endif
    Endif
    'If Right$(datej.Text, 4) & Mid$(datej.Text, 4, 2) & Left$(datej.Text, 2) < Right$(dtex, 4) & Left$(dtex, 2) & Mid$(dtex, 4, 2) Then
    Try numecr = "numecriture"
    'Else
    'Try numecr = "numecriture1"
    'Endif
  End With

End

Public Sub Button1_KeyPress()

  Button1_Click()

End

Public Sub Button1_Click()

  If $process Then Return
  Colbl.Clear
  Coldetail.Clear
  Quittedate(datej)
  If Not datei.ReadOnly And b = 0 Then Quittedate(datei)
  If B <> 1 Then
    requete()
    recup_bl()
  Else
    b = 0
  Endif

End

Public Sub requete()          'construction de la requete sql

  Dim dteN, dateinf, jour As String
  Dim bcond As Boolean
  Dim d As Integer
  Dim dte As Date

  'gestion des dates
  dteN = Right$(datej.Text, 4) & Mid$(datej.Text, 4, 2) & Left$(datej.Text, 2)
  If combregr.Current.Text = "Sans" Then
    $sreq = utils.db.Subst(" AND datebl<=&1 AND cli_regr=&2", dteN, Left(combregr.Current.Text, 1))
  Else
    dateinf = Right$(datei.Text, 4) & Mid$(datei.Text, 4, 2) & Left$(datei.Text, 2)
    $sreq = utils.db.Subst(" AND (datebl BETWEEN &1 AND &2) AND cli_regr=&3", dateinf, dteN, Left(combregr.Current.Text, 1))
    jour = Left(datei.Text, 2)
    bcond = (combregr.Current.Text = "Décade" And jour <> "01" And jour <> "11" And jour <> "21") Or (combregr.Current.Text = "Quinzaine" And jour <> "16" And jour <> "01") Or (combregr.Current.Text = "Mensuel" And jour <> "01")
    If bcond Then
      Message.Error("incohérence dans les dates de début de période, Veuillez vérifier", "OK")
      datei.SelectAll
      datei.SetFocus
    Endif
    dte = Date(Right$(datej.Text, 4), Val(Mid$(datej.Text, 4, 2)), Val(Left$(datej.Text, 2)))
    d = utils.lastday(dte)
    jour = Left(datej.Text, 2)
    bcond = (combregr.Current.Text = "Décade" And jour <> "10" And jour <> "20" And jour <> Str(d)) Or (combregr.Current.Text = "Quinzaine" And jour <> "15" And jour <> Str(d)) Or (combregr.Current.Text = "Mensuel" And jour <> Str(d))
    If bcond Then
      Message.Error("incohérence dans les dates de fin de période, Veuillez vérifier", "OK")
      datei.SelectAll
      datei.SetFocus
    Endif
  Endif
  'tournées
  If Labeltour.Text Then
    $sreq &= utils.db.Subst(" AND cli_tour=&1", Labeltour.Text)
  Endif
  'type client
  If Combocli.Current.Text <> "Tous" Then
    $sreq &= utils.db.Subst(" AND cli_typec=&1", Left(Combocli.Current.Text, 2))
  Endif
  'commercial
  If Combocom.Current.Text <> "Tous" Then
    $sreq &= utils.db.Subst(" AND cli_comm=&1", Left(Combocom.Current.Text, 2))
  Endif

End

Public Sub Button5_Click()

  
  Me.close

End

Public Sub Radiobutton1_Click()

  codeclient.Background = &HD4D0C8& '&HF9F9BD&
  codeclient.ReadOnly = True
  Colbl.clear
  Coldetail.clear


End

Public Sub Radiobutton2_Click()

  Dim Frmt As New String[]

  Frmt = Utils.FColr(Start.LocalSettings["/Coul/Znss"])
  If Start.LocalSettings["/Soc" & Start.Societe & "/Coul_fen"] Then codeclient.Background = Val(Frmt[0])
  codeclient.ReadOnly = False
  Colbl.clear
  Coldetail.clear
  codeclient.SetFocus

End

Public Sub codeclient_KeyPress()

  If Key.code = Key.enter Or Key.code = Key.return Or Key.code = Key.Tab Then
    rResult = Utils.db.Exec("select * from " & Cbase.Table("TabCli") & " where cli_code = &1", codeclient.Text)
    If rResult.Available Then
      Button1.SetFocus()
    Else
      Message.Error("Ce client n'existe pas !")
      codeclient.Select
      codeclient.SetFocus
    Endif
  Endif

End

Public Sub Togglebutton1_Click()

  Dim MyForm As Triclients

  If Radiobutton2.Value = False Then Radiobutton2.Value = True
  'compteTiers()
  bsel = False
  MyForm = New Triclients("Facturefm")
  MyForm.Showmodal()
  If Bsel = True Then
    codeclient.Text = Cclient
  Else
    Codeclient.SetFocus
  Endif

End

Public Sub dte_LostFocus()

  If Not Last.ReadOnly Then
    Last.text = Utils.Cdate_calc(Last.text)
    Last.Text = Utils.Cdate_aff(Last.Text)
    If Last.Text = "00.00.0:00" Or If Last.Text = ".." Then
      Last.Text = Format$(Now, "dd.mm.yyyy")
      Last.SetFocus
    Endif
  Endif

End

Public Sub dte_DblClick()

  If DateChooser1.visible = False Then
    If Not Last.ReadOnly Then
      DateChooser1.visible = True
      DateChooser1.Raise
      DateChooser1.Tag = Last
    Endif
  Else
    DateChooser1.Visible = False
  Endif

End

Public Sub DateChooser1_Activate()

  DateChooser1.tag.text = Format$(DateChooser1.Value, "dd.mm.yyyy")
  DateChooser1.Visible = False

End

Public Sub dte_KeyPress()

  If Key.code = Key.Escape Then
    If DateChooser1.Visible = True Then DateChooser1.Visible = False
  Endif
  If Key.code = Key.F2 Then
    If DateChooser1.Visible = True Then
      DateChooser1.Visible = False
    Else
      If Not Last.ReadOnly Then
        DateChooser1.Visible = True
        DateChooser1.Raise
        DateChooser1.Tag = Last
      Endif
    Endif
  Endif

End

Public Sub Divers_Click()

  Button1_Click()

End

Public Sub Buttour_Click()

  Dim rtour As Rechtour

  rtour = New Rechtour As "tourne"
  rtour.Showmodal

End

Public Sub tourne_trouve(value As String)

  Labeltour.Text = value

End

Public Sub combregr_Click()

  If combregr.Current.Text = "Sans" Then
    datei.Clear
    datei.Background = &HD4D0C8&
    datei.ReadOnly = True
  Else
    datei.Background = Color.TextBackground
    datei.ReadOnly = False
    datei.SetFocus
    datei.SelectAll
  Endif

End

Public Sub Button9_Click()
  
  Dim otpf As OptFac
  Dim m As String
  
  If Colbl.Count <> 0 Then
    If Checentr.Value Then m = "M"
    otpf = New OptFac(ObjFac.finmois, "", "", Me.Parent, " ", False, LDate(datej.Text))
    otpf.Showmodal
    Button1_Click()
  Else
    Message.Warning("Veuillez lancer votre traitement avant d'imprimer SVP ! ")
  Endif

End
